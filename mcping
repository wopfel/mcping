#!/usr/bin/perl

################################################################
#
#  Copyright (C) 2019 Bernd Arnold
#  https://github.com/wopfel/mcping
#
#  Ping multiple hosts, curses interface
#
#  See file LICENSE for license information
#
################################################################

use strict;
use warnings;

use Curses;
use Time::HiRes qw( usleep );
use Net::Ping;
use Getopt::Long;


################################################################
##
##  Global variables
##

# All hosts which should be checked
my @hostlist;

# Options
my $opt_h;
my $opt_loop_limit = 0;            # Limit the number of loops. Default: no limit (program runs endlessly)

# Screen settings
my $first_line_y = 5;


################################################################
##
##  Subroutines
##

sub assert( $ ) {
    my $condition = shift;
    return if $condition;
    my ( $pkg, $file, $line ) = caller( 0 );
    die "Assertion failed! Line: $line.";
}

sub usage( $ ) {
    my $msg = shift;

    if ( $msg ) {
        print "Error: $msg.\n\n";
    }

    print "Usage: $0  hostname  [hostname [...] ].\n";
    exit 1;
}

sub print_help() {

    my $msg = qq{Usage: $0  hostname  [hostname [...] ]
        
        Ping multiple hosts, curses interface
        Copyright (c) 2019 Bernd Arnold
        See readme on https://github.com/wopfel/mcping/blob/master/README.md
        
        OPTIONS
        -h, --help: Show this help text
        -l, --looplimit LIMIT: End the program after LIMIT loops; default (0) is endless
        };

    # Strip white space at the beginning
    $msg =~ s/^ {8}//mg;
    print $msg;

}

sub check_args() {

    Getopt::Long::Configure('bundling');
    my $getopt_status = GetOptions(
        "h"   => \$opt_h,              "help"             => \$opt_h,
        "l=i" => \$opt_loop_limit,     "opt_loop_limit=i" => \$opt_loop_limit,
    );

    if ( ! $getopt_status ) {
        usage "cannot process arguments";
    }

    if ( $opt_h ) {
        print_help();
        exit 1;
    }

    usage "loop limit must be 0 or greater"  if  $opt_loop_limit < 0;

    # No sense if no hosts specified
    if ( scalar @ARGV == 0 ) {
        usage "no hosts defined";
    }

    # Take host list from command line args
    @hostlist = @ARGV;

}

sub draw_initial_screen( $ ) {

    my $win = shift;

    $win->addstr( 2, 0, "Hit Ctrl+C to exit anytime." );

    $win->addstr( $first_line_y - 1,  0, "Host address" );
    $win->addstr( $first_line_y - 1, 15, "Current [ms]" );
    $win->addstr( $first_line_y - 1, 30, "History graph" );
    $win->addstr( $first_line_y - 1, 55, "Success" );
    $win->addstr( $first_line_y - 1, 65, "Avg [ms]" );
    $win->addstr( $first_line_y - 1, 75, "Rcvd#" );

    # Print host names / IP addresses
    my $host_nr = 0;
    foreach my $host ( @hostlist ) {
        $win->addstr( $first_line_y + $host_nr, 0, $host );
        $host_nr++;
    }
    $win->refresh;

}


################################################################
##
## Main program
##

# Check command line args
check_args;

# Initialize the screen
initscr;
my $win = new Curses;

# Initialize ping environment (method, timeout)
my $ping = Net::Ping->new( "icmp", 0.4 );
$ping->hires();

# The history for each host
my @host_history;
$host_history[$_] = "" for (0 .. scalar @hostlist);
# Duration history for each host
my @duration_history;
$duration_history[$_] = () for (0 .. scalar @hostlist);
# Received pings for each host
my @received_pings;
$received_pings[$_] = 0 for (0 .. scalar @hostlist);

# Draw initial screen
draw_initial_screen( $win );

my $loop_nr = 0;

# Endless loop
while (1) {

    # Strip history for each host
    # No screen update here / defer update after next ping check
    my $host_nr = 0;
    foreach my $host ( @hostlist ) {
        $host_history[ $host_nr ] = substr $host_history[ $host_nr ], -20;
        $win->addstr( $first_line_y + $host_nr, 30, $host_history[ $host_nr ] . " " );
        $host_nr++;
    }

    # Ping each host
    $host_nr = 0;
    foreach my $host ( @hostlist ) {

        # Ping this host
        my ( $ret, $dur, $ip ) = $ping->ping( $host );

        # Record history and limit size
        push @{ $duration_history[ $host_nr ] }, ( $ret ? $dur : undef );
        shift @{ $duration_history[ $host_nr ] }  while  ( scalar( @{ $duration_history[ $host_nr ] } ) > 20 );

        # Count successful pings
        $received_pings[ $host_nr ]++  if  $ret;
        my $received_pings_s = sprintf( "%5d", $received_pings[ $host_nr ] );

        # Demo purpose
        #$ret = rand() > 0.1;
    
        # History diagram
        $host_history[ $host_nr ] .= $ret ? "." : "-";

        # Calculate average duration
        my $avg_duration = 0;
        my $avg_duration_count = 0;
        for my $i ( @{ $duration_history[ $host_nr ] } ) {
            # Skip undefined values (resulting from a missed ping)
            next unless $i;
            $avg_duration += $i;
            $avg_duration_count++;
        }
        $avg_duration /= $avg_duration_count  if  $avg_duration_count > 0;
        my $avg_duration_s = sprintf( "%9.4f", $avg_duration * 1000 );
        $avg_duration_s = " " x 9  if  $avg_duration == 0;

        # Calculate success rate
        my $successful_pings = 0;
        my $ping_count = length( $host_history[ $host_nr ] );
        for my $i ( 1 .. $ping_count ) {
            my $char = substr( $host_history[ $host_nr ], $i - 1, 1 );
            $successful_pings++ if $char eq ".";
        }
        my $successrate_s;
        # At this point, a previous ping has occurred so the count should be greater than 0
        assert ( $ping_count > 0 );
        $successrate_s = sprintf( "%5.1f%%", $successful_pings / $ping_count * 100 );

        # Screen updates
        my $dur_s = "";
        if ( $ret ) {
            # The duration, printed in milliseconds
            $dur_s = sprintf( "%9.4f", $dur * 1000 );
        } else {
            # Wipe the (previously shown) timeout-duration if ping was unsuccessful
            $dur_s = " " x 9;
        }
        $win->addstr( $first_line_y + $host_nr, 15, $dur_s );
        $win->addstr( $first_line_y + $host_nr, 30, $host_history[ $host_nr ] );
        $win->addstr( $first_line_y + $host_nr, 55, $successrate_s );
        $win->addstr( $first_line_y + $host_nr, 65, $avg_duration_s );
        $win->addstr( $first_line_y + $host_nr, 75, $received_pings_s );

        # Show screen updates
        $win->refresh;

        # Short sleep before the next try
        usleep( 100_000 );

        $host_nr++;

    }

    $loop_nr++;

    # End loop if limit is reached (0: no limit / endlessly)
    last if $opt_loop_limit != 0 and $loop_nr >= $opt_loop_limit;

}

# Terminate curses
endwin;

